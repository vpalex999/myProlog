/*
9.1. Представление списков. Сортировка
*/

% Сортировка пузырьком
пузырёк(Список,УпорСписок):-
    переставить(Список,Список1),!,  % полезная сортировка?
    пузырёк(Список1,УпорСписок).

пузырёк(УпорСписок, УпорСписок).    % если нет, то список уже упорядочен.


переставить([X,Y|T], [Y,X|T]):-
    больше(X,Y).

переставить([X|T], [X|T1]):-
    переставить(T,T1).

больше(X,Y):-
    X > Y.

/*
Сортировка со вставками

Для того, чтобы упорядочить непустой список L = [X | Хв], необходимо:
(1) Упорядочить хвост Хв списка  L.
(2) Вставить голову X списка L в упорядоченный хвост, поместив ее в такое место,
чтобы получившийся список остался упорядоченным. Список отсортирован.
*/

вставсорт([],[]).

вставсорт([X|Хв], УпорСписок):-
    вставсорт(Хв,УпорХв),
    встав(X,УпорХв,УпорСписок).

% вставить Х на нужное место
встав(X,[Y|УпорСписок], [Y|УпорСписок1]):-
    больше(X,Y),!,
    встав(X,УпорСписок, УпорСписок1).

встав(X,УпорСписок,[X|УпорСписок]).